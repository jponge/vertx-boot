plugins {
  id 'java-library'
  id 'maven-publish'
  id 'com.github.ben-manes.versions' version '0.27.0'
}

repositories {
  mavenCentral()
  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots/'
  }
}

group 'io.github.jponge'
version '1.2.1-SNAPSHOT'

compileJava {
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
}

dependencies {
  api 'io.vertx:vertx-core:3.9.4'
  implementation 'com.typesafe:config:1.4.0'

  testImplementation 'io.vertx:vertx-junit5:3.9.4'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

jar {
  manifest {
    attributes(
      'Implementation-Title': project.name,
      'Implementation-Version': project.version)
  }
}

test {
  useJUnitPlatform()
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
}

publishing {
  publications {
    vertxBoot(MavenPublication) {

      from components.java

      artifact sourceJar {
        classifier "sources"
      }

      artifact javadocJar {
        classifier 'javadoc'
      }

      pom {
        name = 'Vert.x Boot'
        description = 'A Vert.x verticle to boot an application from HOCON configuration.'
        url = 'https://github.com/jponge/vertx-boot'
        licenses {
          license {
            name = 'MIT License'
            url = 'https://opensource.org/licenses/MIT'
          }
        }
        developers {
          developer {
            id = 'jponge'
            name = 'Julien Ponge'
            email = 'julien.ponge@gmail.com'
            url = 'https://julien.ponge.org/'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/jponge/vertx-boot.git'
          developerConnection = 'scm:git:ssh://github.com/jponge/vertx-boot.git'
          url = 'https://github.com/jponge/vertx-boot/tree/master'
        }
      }
    }
  }
  repositories {
    maven {
      url 'https://api.bintray.com/maven/jponge/vertx-boot/vertx-boot'
      credentials {
        username project.hasProperty('bintrayRepoUsername') ? bintrayRepoUsername : System.getenv('bintrayRepoUsername')
        password project.hasProperty('bintrayRepoPassword') ? bintrayRepoPassword : System.getenv('bintrayRepoPassword')
      }
    }
  }
}

wrapper {
  gradleVersion = '6.0'
}
